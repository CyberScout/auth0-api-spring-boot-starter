plugins {
    id 'maven'
    id 'java-library'
    id 'io.freefair.lombok' version '5.1.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

group = 'com.cyberscout.oss'
version = '0.6.0'

repositories {
    mavenCentral()
}

dependencies {
    api 'com.auth0:auth0-spring-security-api:1.4.0'
    api 'com.auth0:auth0:1.20.0'
    api 'com.auth0:java-jwt:3.10.3'
    api 'org.springframework.boot:spring-boot-autoconfigure:2.3.3.RELEASE'
    api 'org.springframework.boot:spring-boot-starter-security:2.3.3.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-web:2.3.3.RELEASE'
    implementation 'com.auth0:jwks-rsa:0.12.0'
    compileOnly 'org.springframework.boot:spring-boot-configuration-processor:2.3.3.RELEASE'
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.3.3.RELEASE') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.security:spring-security-test:5.3.4.RELEASE'
}

//
// Maven Publishing
//
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def publicGitUrl = "https://github.com/CyberScout/${project.name}.git"
def privateGitUrl = "git@github.com:CyberScout/${project.name}.git"
def githubRepo = "CyberScout/${project.name}"
def githubUrl = "https://github.com/${githubRepo}"

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = project.group
            artifactId = project.name
            version = project.version
            pom {
                name = 'Auth0 API Spring Boot Starter'
                description = 'Spring Boot starter to get an API integrated with Auth0'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'cyberscout'
                        name = 'CyberScout, LLC'
                        email = 'dev-support@cyberscout.com'
                        url = 'https://cyberscout.com'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = "${githubUrl}/issues"
                }
                scm {
                    connection = publicGitUrl
                    developerConnection = privateGitUrl
                    url = githubUrl
                }
            }
        }
    }
}

//
// Bintray Publishing
//
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true
    override = true
    pkg {
        userOrg = 'cyberscout'
        repo = 'cyberscout-oss-maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = publicGitUrl
        githubRepo
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}

//
// Uber-Publish Task
//
task cdPublish(dependsOn: [check, bintrayUpload]) {
    description 'Publish artifacts to distribution points'
    group 'publishing'
}

bintrayUpload.mustRunAfter check
